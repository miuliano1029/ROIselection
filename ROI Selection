//2024: UPDATE by M.Iuliano. This macro was modified for a few reasons. 
//(1) Scale for length measurements was from a previous correction. Macro now prompts for image scale at the beginning, allowing for use across different images.
//(2) Naming ROIs individually, making it easier to identify the appropriate item if one needs to go back and re-measure lengths.
//(3) This now allow you to set an input folder rather than opening up each file individually. However, you need to start with a folder containing only the TIF files
//(4) Option to record LENGTH, AREA, or BOTH

//this macro is to select ROI and split them by channel, saving each channel and a merged version in their own folders

//the basic process is that it asks you to open all the images you want ROI from, select a folder
//to save the output ROI images in (creating subfolders for each channel) as well as the zip file
//for the ROImanager metadata detailing where in the image the ROI were taken from
//once complete, the lengths or area will also be printed and exported to an excel file

input = getDirectory("Choose folder for starting images");
ROIdir = getDirectory("Choose folder for ROI files");
File.setDefaultDir(ROIdir);
list = getFileList(input);
list = Array.sort(list);
suffix = ".tif";

Dialog.create("Experiment Info");
		Dialog.addString("Title","Experiment Title",10);
		Dialog.addNumber("Pixel Scale:", 0.10049847,8,5,"(um/pixel)");
//commonly used scale values can be included here for convenience
Dialog.show();
experiment = Dialog.getString();
scale = Dialog.getNumber();
print("\\Clear")
roiManager("reset");
//allows for preferential selection tool
//can measure ROI length, area, or both. Length will prompt a line selection for measurement
Dialog.create("Channel Info");
	choices = newArray("rectangle","polygon","freehand");
	choices2=newArray("length","area","both");
	Dialog.addChoice("What shape ROI?",choices,"polygon");
	Dialog.addChoice("Record",choices2,"length");
	Dialog.addNumber("How many starting channels?",4,0,1,"channel/s");
	Dialog.addNumber("How many final channels?",4,0,1,"channel/s");
Dialog.show();
choice = Dialog.getChoice();
choice2 = Dialog.getChoice();
numChan = Dialog.getNumber();
mergeChan= Dialog.getNumber();
//Assigning channel may be important for future processing, so here you can assign this order following the RGBC/1234 order
Dialog.create("Assign Colors to Each Channel");
	channels = newArray("Red","Green","Blue","Cyan");
	chan= newArray("C1","C2","C3","C4");
	color=Array.trim(channels, numChan);
	for(n=0;n<numChan;n++){
		Dialog.addChoice("Channel "+(n+1),color,color[n]);
		Dialog.addString("", "");
	}
			Dialog.addMessage("Merged file order channels to R-G-B-C");
			Dialog.addMessage("2=RG 3=RGB 4=RGBC");
		
Dialog.show();
chanArray = newArray();
signalArray=newArray();
for(n=0;n<numChan;n++) {
	chanArray = Array.concat(chanArray,Dialog.getChoice());
	signalArray = Array.concat(signalArray,Dialog.getString());
	if(File.exists(input+chanArray[n])!=1) {
		File.makeDirectory(ROIdir+File.separator+chanArray[n]+" - "+signalArray[n]);
	}
}
oldchan = "Channel Organization\n";
		for(n=0;n<numChan;n++) {
			oldchan =oldchan+chanArray[n]+" = "+signalArray[n]+"\n";
		}		
		
//you will get a csv file with the channel order from the origial TIF as well as the new sequence for the ROI
getdateandtime();
print(oldchan);
print("Merged file order channels to R-G-B-C");
print("2 Channel=RG 3 Channel=RGB 4 Channel=RGBC");


merge = ROIdir+"Merge";
File.makeDirectory(merge);

zipfile = ROIdir+"ROIs";
File.makeDirectory(zipfile);

processFolder(input);
selectWindow("Log");
saveAs("Text", ROIdir+ experiment +" Channel_Info.csv");

function processFolder(input) {
filelist = getFileList(input);
filelist = Array.sort(filelist);

for (i = 0; i < filelist.length; i++) {
	if(File.isDirectory(input + File.separator + filelist[i]))
		processFolder(input + File.separator + filelist[i]);
	if(endsWith(input + File.separator + filelist[i], suffix))
		processFile(input, ROIdir, filelist[i]);
 }
}
function processFile(input, ROIdir, file)  {
open(input+file);
setMinAndMax(0, 65535);
run("Channels Tool...");
Property.set("CompositeProjection", "Sum");
Stack.setDisplayMode("composite");
run("Color Balance...");
run("Enhance Contrast", "saturated=0.35");
run("Properties...", "channels="+numChan+" slices=1 frames=1 unit=micron pixel_width="+scale+" pixel_height="+scale+"");
	title = getTitle();
	//allows you to check the image before committing to selection
	waitForUser ("Check Image");
	//creates a loop for multiple rois in one image
	roiManager("reset");
	repeat=	getBoolean("Do you have a new ROI in frame?");
while (repeat==1) {
    // Code to execute if the user clicked "Yes"
  		run("Original Scale");
		title = getTitle();
		//this is the function, see the bottom of the macro
    if (choice2 == "area"){
    	ROIarea();}
 	if (choice2 == "length"){
		ROIlength();}
	if (choice2 == "both"){
		ROIboth();}
	repeat=	getBoolean("Do you have a new ROI in frame?");
	}
	if(repeat !=1) {
		run("ROI Manager...");
		if(roiManager("count") > 0) {
			roiManager("Save", zipfile+File.separator+title+"_ROI.zip");
			close();
		}
		else {
			close();
		}
	}
}

//this function prompts you to create the ROI it will then measure the region selected and
//copy it in each channel and save each channel as an individual image with the color in the name

function	ROIlength(){
	run("Set Measurements...", "display redirect=None decimal=5");
	setTool(choice);
	title=getTitle();
	selectWindow(title);
	waitForUser ("Select ROI then hit OK");
	roiManager("Add");
	roiManager("Show All");
	count = roiManager("count");
	roiManager("Select", count-1);
	num = (count+1)/2;
    roiManager("Rename", title+" ROI "+num);
	selectWindow(title);
	c=0;
	for(l=0;l<numChan;l++) {	
		setSlice(l+1);
		run("Copy");
		run("Internal Clipboard");
		selectWindow("Clipboard");
		run(chanArray[l]);
		count = roiManager("count");
		num = (count+1)/2;
		saveAs("Tiff", ROIdir+File.separator+chanArray[l]+" - "+signalArray[l]+File.separator+title+" "+num+" "+chanArray[l]+" "+signalArray[l]);
		rename(chanArray[l]);
		selectWindow(title);
		c=c+1;
	}
if (mergeChan == 2){
	run("Merge Channels...", "c1=Red c2=Green create ignore");
	saveAs("Tiff", merge+File.separator+title+" "+num);
	close();
}
if (mergeChan == 3){
	run("Merge Channels...", "c1=Red c2=Green c3=Blue create ignore");
	saveAs("Tiff", merge+File.separator+title+" "+num);
	close();
}
if (mergeChan == 4){
	run("Merge Channels...", "c1=Red c2=Green c3=Blue c4=Cyan create ignore");
	saveAs("Tiff", merge+File.separator+title+" "+num);
	close();
}

	selectWindow(title);
	close("\\Others");
	if(choice == "freehand"){
	setTool("freeline");}
	else {
	setTool("polyline");}
	
		waitForUser ("Measure dendrite length");
		roiManager("Add");
		count = roiManager("count");
		roiManager("Select", count-1);
		n = count/2;
				roiManager("Rename", title+" Length "+n);
				run("Measure");
			length = getResult("Length",0);	
			nres=nResults-1;
			setResult("Label", nres, title+" "+n);
			updateResults();
	}
	
function ROIarea(){
	run("Set Measurements...", "area display redirect=None decimal=5");
	setTool(choice);
	title=getTitle();
	selectWindow(title);
	waitForUser ("Select ROI then hit OK");
	roiManager("Add");
	roiManager("Show All");
	count = roiManager("count");
	roiManager("Select", count-1);
	num = (count);
    roiManager("Rename", title+" ROI "+count);
	selectWindow(title);
	c=0;
	for(l=0;l<numChan;l++) {	
		setSlice(l+1);
		run("Copy");
		run("Internal Clipboard");
		selectWindow("Clipboard");
		run(chanArray[l]);
		count = roiManager("count");
		saveAs("Tiff", ROIdir+File.separator+chanArray[l]+" - "+signalArray[l]+File.separator+title+" "+num+" "+chanArray[l]+" "+signalArray[l]);
		rename(chanArray[l]);
		selectWindow(title);
		c=c+1;
	}
if (numChan == 2){
	run("Merge Channels...", "c1=Red c2=Green create ignore");
	saveAs("Tiff", merge+File.separator+title+" "+num);
	close();
}
if (numChan == 3){
	run("Merge Channels...", "c1=Red c2=Green c3=Blue create ignore");
	saveAs("Tiff", merge+File.separator+title+" "+num);
	close();
}
if (numChan == 4){
	run("Merge Channels...", "c1=Red c2=Green c3=Blue c4=Cyan create ignore");
	saveAs("Tiff", merge+File.separator+title+" "+num);
	close();
}
selectWindow(title);
		roiManager("Select", count-1);
		run("Measure");		
}

function ROIboth(){
	run("Set Measurements...", "area display redirect=None decimal=5");
	setTool(choice);
	title=getTitle();
	selectWindow(title);
	waitForUser ("Select ROI then hit OK");
	roiManager("Add");
	roiManager("Show All");
	count = roiManager("count");
	roiManager("Select", count-1);
	num = (count+1)/2;
    roiManager("Rename", title+" ROI "+num);
    roiManager("Select", count-1);
		run("Measure");	
	selectWindow(title);
	c=0;
	for(l=0;l<numChan;l++) {	
		setSlice(l+1);
		run("Copy");
		run("Internal Clipboard");
		selectWindow("Clipboard");
		run(chanArray[l]);
		count = roiManager("count");
		num = (count+1)/2;
		saveAs("Tiff", ROIdir+File.separator+chanArray[l]+" - "+signalArray[l]+File.separator+title+" "+num+" "+chanArray[l]+" "+signalArray[l]);
		rename(chanArray[l]);
		selectWindow(title);
		c=c+1;
	}
if (numChan == 2){
	run("Merge Channels...", "c1=Red c2=Green create ignore");
	saveAs("Tiff", merge+File.separator+title+" "+num);
	close();
}
if (numChan == 3){
	run("Merge Channels...", "c1=Red c2=Green c3=Blue create ignore");
	saveAs("Tiff", merge+File.separator+title+" "+num);
	close();
}
if (numChan == 4){
	run("Merge Channels...", "c1=Red c2=Green c3=Blue c4=Cyan create ignore");
	saveAs("Tiff", merge+File.separator+title+" "+num);
	close();
}
	selectWindow(title);
	if(choice == "freehand"){
		setTool("freeline");
	}
	else{
	setTool("polyline");}
		waitForUser ("Measure dendrite length");
		roiManager("Add");
		count = roiManager("count");
		roiManager("Select", count-1);
		n = count/2;
				roiManager("Rename", title+" Length "+n);
				run("Measure");
			length = getResult("Length",0);	
			nres=nResults-1;
			setResult("Label", nres, title+" "+n);
			updateResults();
	}
	
	 function getdateandtime() {
     MonthNames = newArray("Jan","Feb","Mar","Apr","May","Jun","Jul","Aug","Sep","Oct","Nov","Dec");
     DayNames = newArray("Sun", "Mon","Tue","Wed","Thu","Fri","Sat");
     getDateAndTime(year, month, dayOfWeek, dayOfMonth, hour, minute, second, msec);
     TimeString ="Date: "+DayNames[dayOfWeek]+" ";
     if (dayOfMonth<10) {TimeString = TimeString+"0";}
     TimeString = TimeString+dayOfMonth+"-"+MonthNames[month]+"-"+year+"\nTime: ";
     if (hour<10) {TimeString = TimeString+"0";}
     TimeString = TimeString+hour+":";
     if (minute<10) {TimeString = TimeString+"0";}
     TimeString = TimeString+minute+":";
     if (second<10) {TimeString = TimeString+"0";}
     TimeString = TimeString+second;
       print(TimeString);
  }
	
selectWindow("Results");
saveAs("Text", ROIdir + " " + experiment +" ROI Measurements.csv");
run("Clear Results");
roiManager("reset");
